---
title: ABC338参加記録
# description: 

date: 2024-01-28
# lastmod: yyyy-mm-dd
# hidedate: true

# ogimage: https://hoge/fuga/piyo.img

draft: true

tags:
  - 競技プログラミング
  - コンテスト参加記録
archives:
  - 2024
  - 2024-01
# sample
# - yyyy
# - yyyy-mm

math: true
# toc: false
# build: {list: never}
---

## はじめに
本稿は2024-01-27に開催された[ABC338](https://atcoder.jp/contests/abc338)の参加記録です。

## 戦績
今回は、ABCDEF6完(338th)で、パフォーマンス<span style="color:blue">1969</span>、レーティング変化は<span style="color:cyan">1317</span>&rarr;<span style="color:cyan">1401</span>(+84)でした。

## 所感
奇跡的な上振れを引きました。
後ろ2問はかなり「こうしたらもしかしたら行けるかもな～」が通ってしまった感があります。
D問題を本番で解けたのは成長を感じました。

## 雑解法

### A - Capitaliazed?
[問題へのリンク](https://atcoder.jp/contests/abc338/tasks/abc338_a)

#### 問題
英大文字・小文字からなる空でない文字列$S$に対して、次の条件が満たされているか判定せよ。

- $S$の先頭は大文字であり、それ以外はすべて小文字

#### 解法
文字列の大文字、小文字判定ができればACできます。多くの環境で英大文字、小文字はASCIIコードに対応しているため、`if ('A' <= c && c <= 'Z')`のような条件式で判定できます。

```d
import std;

void main () {
    string S = readln.chomp;
    bool ok = true;
    if ( !('A' <= S[0] && S[0] <= 'Z') ) ok = false;
    foreach (c; S[1..$]) if ('A' <= c && c <= 'Z') ok = false;

    if (ok) {
        writeln("Yes");
    }
    else {
        writeln("No");
    }
}

void read (T...) (string S, ref T args) {
    auto buf = S.split;
    foreach (i, ref arg; args) {
        arg = buf[i].to!(typeof(arg));
    }
}
```

### B - Frequency
[問題へのリンク](https://atcoder.jp/contests/abc338/tasks/abc338_b)

#### 問題
英子文字からなる文字列$S$が与えられます。$S$に最も多く出現する文字を求めてください。そのような文字が複数ある場合は、そのうちアルファベット順で最も早いものを答えてください。

#### 解法
各文字の出現回数をカウントする必要があり、少し面倒です。
どんな方針をとるにしろ、各文字を数字に対応させることで比較的容易に解くことができます。
例えば、

文字a, b, ..., zに対して、順番に次を行う : $S$に何回出現したかをカウントし、今までの最大値を更新したら答えをその値で更新する。

などが思いつきます。
いずれにしろ、ナイーブに操作しても十分間に合うので頑張りましょう。
```d
import std;

void main () {
  string S = readln.chomp;
  int MAX = 0;
  char ans;

  foreach (i; 0..26) {
  int count = 0;
    foreach (c; S) {
      if (c == 'a'+i) count++;
    }

    if (MAX < count) {
      MAX = count;
      ans = ('a'+i).to!char;
    }
  }

  writeln(ans);
}
```

### C - Leftover Recipes
[問題へのリンク](https://atcoder.jp/contests/abc338/tasks/abc338_c)

#### 問題
$N$種類の材料があり、$i$番目の材料は$Q\_i$グラムである。

料理Aは1人分を作るために$i$番目の材料が$A\_i$グラム必要で、料理Bは1人分を作るために$i$番目の材料が$B\_i$グラム必要である。
ただし、整数人数分しか作れない。

最大何人分の料理が作れるかを求めよ。

- $1 \leq N \leq 10$
- $1 \leq Q\_i \leq 10^6$
- $0 \leq A\_i \leq 10^6$
- $1 \leq A\_i$なる$i$が存在する。
- $0 \leq B\_i \leq 10^6$
- $1 \leq B\_i$なる$i$が存在する。

#### 解法
料理が1種類しかないなら比較的楽に解けます。
それなら、片方の料理を何個作るかを固定して考えられないかを検討してみましょう。

実はこの方針で押し切ることができます。
料理Aを$x$人分作るとして考えましょう。
この時残った材料は$Q\_i - x \times A\_i$グラムです。
残った材料で作れる料理Bの最大値は、$\min\_{1 \leq i \leq N} \lfloor \frac{Q\_i - x \times A\_i}{B\_i} \rfloor$になります。(ただし、$B\_i = 0$の時は$\infty$だとします。)

この値の最大値をとれば答えになっているはずです。
$x$を一つ決めたとき、料理Bを作れる量の最大値は$O(N)$時間で求めることができるため、$O(10^6 N)$時間で解を求めることができます。
$N$が小さいのが決め手でしたね。

```d
import std;

void main () {
    int N = readln.chomp.to!int;
    auto Q = readln.split.to!(int[]);
    auto A = readln.split.to!(int[]);
    auto B = readln.split.to!(int[]);

    solve(N, Q, A, B);
}

void solve (int N, int[] Q, int[] A, int[] B) {
    // 料理Aの量を決め打ち -> Bを出す
    int ans = 0;

    for (int x = 0; x <= 10^^6; x++) {
        // 料理A
        auto q = Q.dup;
        bool no = false;
        foreach (j; 0..N) {
            q[j] -= x*A[j];
            if (q[j] < 0) no = true;
        }

        if (no) break;

        // 料理B -> floorをとればいい
        int y = int.max;
        foreach (j; 0..N) {
          if (B[j] == 0) continue;
          y = min(y, q[j]/B[j]);
        }

        ans = max(ans, x+y);
    }

    writeln(ans);
}

void read (T...) (string S, ref T args) {
    auto buf = S.split;
    foreach (i, ref arg; args) {
        arg = buf[i].to!(typeof(arg));
    }
}
```

### D - Island Tour
[問題へのリンク](https://atcoder.jp/contests/abc338/tasks/abc338_d)

#### 問題
$1$から$N$まで番号付けされた島があり、$i$番目の島と$i+1$番目の島を結ぶ$N$本の橋があります。($N$番目の島は$1$番目の島との橋があります)
また、数列$X = (X\_1, X\_2, \dots, X\_M)$が与えられます。

ここで、数列$a = (a\_1, a\_2, \dots, a\_l)$であって、次の条件を満たすものを「ツアー」といいます。
- $a\_1 = X\_1, a\_l = X\_M$
- 島$a\_i$と$a\_{i+1}$は直接橋でつながれている
- $X$は$a$の部分列である。

ツアー$a = (a\_1, a\_2, \dots, a\_l)$の長さを$l-1$とします。一つだけ橋を封鎖するとき、ツアーの長さの最小値を求めよ。

#### 解法
橋を封鎖した後、封鎖する前の最短ツアーに対して、$X\_i$から$X\_{i+1}$において通る必要のある橋の数がどうなるかに着目しましょう。
島の配置が次のようになっているとき、島$1$から$7$に行く場合を実際に見てみます。
<pre>
遠回り(6手) <- -> 最短(2手)
              1  8
            2      7
            
            3      6
              4  5
</pre>
この場合、橋$(1, 8)$や橋$(7, 8)$を封鎖するとすると4手損します。
そうでない場合、最短を通ることができます。
これは橋$(1, 2), (2, 3), \dots, (6, 7)$を封鎖したときのコストが2増えたと解釈することができます。

これをすべての$X\_i, X\_{i+1}$に対して行うことで、「ある橋を封鎖すると合計何手損するか？」を求めることができます。

コストが増える橋は一つの$X\_i, X\_{i+1}$に対して$O(N)$個あるので、$O(NM)$時間かかってしまいますが、
コストが増える橋は連続しているという性質を使うことで、imos法による累積ができます。

したがって、一回当たり$O(1)$、最後に$O(N)$かけて正しい累積結果を得ることができ、$O(N+M)$時間で問題を解くことができます。

```d
import std;

void main () {
    int N, M; readln.read(N, M);
    auto X = readln.split.to!(int[]);
    X[] -= 1;

    solve(N, M, X);
}

void solve (int N, int M, int[] X) {
    // 切っていなかったときのことに思いをはせる -> 円周みたいになってるから、右回りor左回りが最適だった
    // ある辺をカットしたときの怨念の総和を見たい。
    // -> そこを通るときにカットされると+なんぼいくよというのはわかるので、累積しておけばよくないか？
    // 累積もナイーブにやると死ぬので、imosチックにやる(回ってしまう場合には先頭にも積んでおく)

    long cost = 0;
    auto cum = new long[](N);
    // cum[i] := (i, i+1)を結ぶ辺

    int cur = X[0];
    foreach (i; 1..M) {
        // cur->X[i]の最短
        int x = abs(cur-X[i]);
        int y = N - x;
        cost += min(x, y);

        int l = max(cur, X[i]);
        int s = min(cur, X[i]);
        if (min(x, y) == x) {
            // またがない
            cum[s] += y-x;
            cum[l] -= y-x;
        }
        else {
            // またぐ場合
            cum[l] += x-y;
            cum[0] += x-y;
            cum[s] -= x-y;
        }

        cur = X[i];
    }

    // imos
    foreach (i; 1..N) {
        cum[i] += cum[i-1];
    }

    long MIN = long.max;
    foreach (c; cum) MIN = min(MIN, c);

    writeln(cost + MIN);
}

void read (T...) (string S, ref T args) {
    auto buf = S.split;
    foreach (i, ref arg; args) {
        arg = buf[i].to!(typeof(arg));
    }
}
```

### E - Chords
[問題へのリンク](https://atcoder.jp/contests/abc338/tasks/abc338_e)

#### 問題
円周上に$2N$個の点が等間隔で並んでいて、ある点から初めて時計回りに$1$から$2N$までの番号がついています。

円周上には$N$個の元があり、$i$個めの弦は$(A\_i, B\_i)$を結んでいます。ただし、すべての$A\_i$及び$B\_i$は相異なります。

弦同士の交点が存在するか判定してください。

#### 解法
弦同士の交点が存在しない前提で考えます。また、弦$(A\_i, B\_i)$に対して$A\_i < B\_i$を仮定します。

弦$(A\_i, B\_i)$を張ったとき、他の弦$(C\_i, D\_i)$は$A\_i, B\_i$の完全に内側または完全に外側である必要があります。
また、実はこの条件は十分です(解説によると十分らしいです)。
すなわち、この条件を守りながら張れるか？を判定する問題になります。

これは弦の端点の小さい方で昇順ソートすることでうまく処理することができます。疲れてきたので実装例を見てください(投げやり)

```d
import std;

void main () {
    int N = readln.chomp.to!int;
    auto lines = new Tuple!(int, int)[](N);
    foreach (i; 0..N) {
        readln.read(lines[i][0], lines[i][1]);
        lines[i][0]--, lines[i][1]--;
    }

    solve(N, lines);
}

void solve (int N, Tuple!(int, int)[] lines) {
    // 交点を作らないための必要条件は
    // - 2の剰余が異なる頂点へつなぐこと。
    // - 一度分割された世界から逃げ出さないこと
    // 十分かはわからないけどやってみる価値あり

    foreach (ref l; lines) if (l[1] < l[0]) swap(l[0], l[1]);
    lines.sort!"a[0] < b[0]";

    bool ok = true;
    DList!int Q;
    int idx = 0;

    foreach (i; 0..2*N) {
        // 世界を解放
        if (!Q.empty && Q.front == i) {
            Q.removeFront;
            continue;
        }

        auto l = lines[idx];
        idx++;

        if (l[0] % 2 == l[1] % 2) ok = false; // 条件1
        if (!Q.empty && Q.front < l[1]) ok = false; // 条件2

        if (!ok) break;

        Q.insertFront(l[1]);
    }

    if (ok) {
        writeln("No");
    }
    else {
        writeln("Yes");
    }
}

void read (T...) (string S, ref T args) {
    auto buf = S.split;
    foreach (i, ref arg; args) {
        arg = buf[i].to!(typeof(arg));
    }
}
```


